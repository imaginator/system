import org.openhab.core.model.script.ScriptServiceUtil

rule "KNX Volume Contol"
when
    Member of gKNXVolume received command
then
    logInfo("KNXVolumeControl","triggered: {}",triggeringItem.name)
    var room = triggeringItem.name.replace("KNXVolume","") 
    logInfo("KNXVolumeControl","extracted room: {}", room)
    var String ChromecastDeviceName = "ChromecastAudio" + room + "Volume" 
    logInfo("KNXVolumeControl","Will change volume on: {}", ChromecastDeviceName)
    var CurrentVolume = ScriptServiceUtil.getItemRegistry.getItem(ChromecastDeviceName).state as DecimalType
    logInfo("KNXVolumeControl","CurrentVolume: {}", CurrentVolume)
    
    if (receivedCommand == OFF) {
        sendCommand(ChromecastDeviceName , ((CurrentVolume - 5).toString()))
    }
    else if (receivedCommand == ON) {
        sendCommand(ChromecastDeviceName ,  ((CurrentVolume + 5).toString()))
    }
end

// Trigger ⏯️ and ⏭️ based on long kepresses on the KNX buttons.
// eg: from KNXPlayback<BathroomGuest>
//   to ChromecastAudio<BathroomGuest>Control

rule "KNX Play/Pause/Next"
when
    Member of gKNXPlayback received command
then
    var String room = triggeringItem.name.replace("KNXPlayback","")
    var String ChromecastDeviceName = "ChromecastAudio" + room + "Control"
    var CurrentPlaybackState = ScriptServiceUtil.getItemRegistry.getItem(ChromecastDeviceName).state
    if (receivedCommand == OFF) {
        // toggle play / pause
        if (CurrentPlaybackState == PAUSE) {
           logInfo("KNX Play/Pause/Next", "resuming playback in {}", room)
           sendCommand(ChromecastDeviceName , "PLAY")
        }
        else if (CurrentPlaybackState == PLAY) {
           logInfo("KNX Play/Pause/Next", "pausing in {}", room)
           sendCommand(ChromecastDeviceName , "PAUSE")
        }
    }
    // play next track
    else if (receivedCommand == ON) {
        logInfo("KNX Play/Pause/Next", "skipping track in {}", room)
        // NEXT/PREVIOUS not supported directly in Chomecast binding so using spotify binding
        // another approach would be to to scrub to (tracklength-1s) 
        SpotifyPlayerBridge_TrackPlayer.sendCommand(NEXT)
    }
end
